# shortcut to this dotfiles path is $ZSH
export DOTFILES=$HOME/code/dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=$HOME/code

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

source $DOTFILES/zsh/antigen/antigen.zsh
antigen-bundles <<EOB 
git
debian
bundler
rails3
mvn
EOB
antigen-use oh-my-zsh
antigen-theme blinks
antigen-apply

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files and antigen
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#**/antigen.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# ls on cd
function chpwd() {
    emulate -L zsh
    ls
}

# Fix terminal problems when sshing
case "$TERM" in
    rxvt-unicode-256color)
        TERM=rxvt-unicode
        ;;
esac

# Start x on login
if [[ -z $DISPLAY ]] && [[ $(tty) = /dev/tty1 ]]; then
    exec ssh-agent startx
    exec xinit -- /usr/bin/X -nolisten tcp vt7
fi
